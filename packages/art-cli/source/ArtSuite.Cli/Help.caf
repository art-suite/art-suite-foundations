import &StandardImport, {} &colors

class Help
  @toHelpString: (args...) ->
    compactFlatten args
    .join "\n"
    .trim()

  @getCommandSummary: (commandName, {alias, description, options}) ->
    commandName = dashCase commandName
    @toHelpString
      "" #{colors.bold colors.brightWhite 'Command: #{commandName}'} #{alias && '(#{alias})'}
      "  #{description}" if present description
      ""
      if options then colors.blue("  options: ") + colors.green (array v, k from-object options with k when !v.advanced).sort().join ', '

  @getOptionDetails: (option, details) ->
    switch
    when details is String  then description = details
    when (details is Array) && details.length == 2  then [argument, description] = details
    when details is Object                          then {argument, description, advanced} = details
    else
      log.warn {} option, details
      throw new Error "expecting options details to be string, 2-length array or object"
    @toHelpString
      colors.green("  --#{option} #{colors.yellow argument if argument}") + if advanced then colors.grey " (advanced)" else ''
      "\n    " + description


  @getCommandDetails: (command, {alias, description, options, examples}, cliName) =>
    @toHelpString
      compactFlatten []
        "" #{colors.bold colors.brightWhite 'Command details: #{command}'}
        '(#{alias})' if alias
        colors.blue "[options]" if options
      .join ' '
      "  #{description}" if present description
      ""
      colors.blue "options:\n" if options
      if options
        array option in Object.keys(options).sort() with @getOptionDetails option, options[option]
        .join :\n\n\n

      colors.blue "\nexamples: " if examples?.length > 0
      if examples?.length > 0
        array example, i from examples by 2
          description = examples[i+1]
          []
            ""
            "  " + colors.brightWhite cliName + " " + example
            ""
            "    " + description.replace /\n/g, "\n    "
            ""

  ## getHelp
    IN:
      startFile: <String> name of the CLI app
      help:
        description: <String>
        commands:
          command1Name:
            description:
            options: {} option-name to option-help

            examples: []
              cli-example <string>
              cli-example-description <string>
              ...
      commandName: <String> (optional)
        If provided, show detailed help about this command derived from input help[commandName]

    NOTE: all commandNames are normalized to lowerCamelCaser, so you can pass in a mix.

    option-help:
        description <String>
      OR
        [] argument-name <String>, description <String>
      OR
        argument: <String>
        description: <String>
        advanced: t/f
          If true, hidden except on command-specific help
          Also gets note as "advanced" in help when listed.

  @getHelp: (startFile, help, commandName) =>
    if help?
      help extract description
      commands = object v, k from help.commands with-key lowerCamelCase k

    cliName = &path.basename startFile
    commandSpecificHelp = commands?[lowerCamelCase commandName]

    @toHelpString
      unless commandSpecificHelp
        """
          #{cliName} help:

          Usage: #{colors.green cliName} #{colors.brightWhite :command} #{colors.blue "[options]"}

          #{description}
        .trim()
        + :\n

      if commands
        commands = object v, k from commands with-key lowerCamelCase k
        if commandSpecificHelp
          @getCommandDetails commandName, commandSpecificHelp, cliName
        else
          array commandName from Object.keys(commands).sort()
            @getCommandSummary commandName, commands[commandName]
          .join "\n\n"
          + "" \n\nGet detailed help for any command with:
          + colors.green  " #{cliName}"
          + colors.brightWhite " command"
          + colors.green " --help"


  @getBasicHelp: (commands) ->
    """
      Commands: #{}
        Object.keys commands
        .join ', '
