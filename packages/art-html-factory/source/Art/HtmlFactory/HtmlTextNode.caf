import &ArtStandardLib

class HtmlTextNode extends &ArtClassSystem.BaseClass
  constructor: (@name, @props, @children) ->
    super
    @name = @name.toLocaleLowerCase()
    if hasProperties @props
      if @props.style
        @style = @props.style
        @props.style =
          array value, name from @style
            "#{name}: #{value}"
          .join '; '
    else
      @props = null

  @escapeHtmlString: escapeHtmlString = (string = "") ->
    "#{string}".replace /["<>&]/, (match) ->
      switch match
      when :" then "" &quot;
      when :& then "" &amp;
      when :< then "" &lt;
      when :> then "" &gt;

  @getter
    propsString: ->
      if @props
        ' ' +
          array propValue, propName from @props
            if propValue == true
              propName
            else
              "" #{propName}="#{escapeHtmlString propValue}"
          .join ' '

  compile: (indent) ->
    compactFlatten @_compile indent

  toString: ->
    @compile ''
    .join "\n"

  ##################
  # PRIVATE
  ##################
  applyIndent = (indent, line) ->
    if indent
      indent + line
    else
      line
  _compile: (indent) ->

    switch @name
    when :a, :b
      []
        "" <#{@name}#{@propsString}>
        @_getCompiledChildren()
        "" </#{@name}>
      .join ''
    else
      if compiledChildren = @_getCompiledChildren indent
        []
          applyIndent indent, "" <#{@name}#{@propsString}>
          compiledChildren
          applyIndent indent, "" </#{@name}>
      else if @name == "link" || @name == "meta"
        applyIndent indent, "" <#{@name}#{@propsString}>
      else
        applyIndent indent, "" <#{@name}#{@propsString}></#{@name}>


  _getCompiledChildren: (indent) ->
    if @children?.length > 0
      indent = indent + '  ' if indent?
      array child from @children
        if isString child
          if indent?
            "#{indent}#{child.replace /\n/g, "\n#{indent}"}"
          else
            child
        else
          child._compile indent
