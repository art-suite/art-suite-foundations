import &ArtStandardLib
if getEnv().REAL_SMTP
  log "USING REAL SMTP"
else
  log "USING FAKE, LOCAL SMTP FOR TESTING"
  log "For real smtp:\n> REAL_SMTP=true npm test"
merge
  &ArtStandardLib
  &ArtClassSystem
  &ArtEmailValidator
  &ArtCommunicationStatus

  test: (args...) -> global.test args...

  newTestSocket: (failureResponseString) ->
    unless getEnv().REAL_SMTP
      on: (event, handler) ~>
        (@_handlers ?= {})[event] = handler
        if event == "data"
          nextTick -> handler "" 220 mx.google.com ESMTP b2si3142061edh.379 - gsmtp\n

      destroy: ~>
      setTimeout: (miliseconds) -> assert.isNumber miliseconds
      write: (data, callback) ~>
        assert.ok true, present data
        nextTick ->
          callback?()
          nextTick ->
            @_handlers?.data? if !(found = /^rcpt to:<([^>]+)/.exec data) || found[1] == :shanebdavis@gmail.com
              "" 250 2.1.0 OK b2si3142061edh.379 - gsmtp\n
            else
              failureResponseString ? "" 550-5.1.1 The email account that you tried to reach does not exist.
